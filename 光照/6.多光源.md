# 6.多光源

上一节中，我们已经可以创建多种光源了，包括定向光源、点光源及聚光光源。这一张我们要在我们的场景中模拟多个光源叠加的情况。

其实很简单，思想就是跟我们之前的光的分量叠加时做加法是一样的，我们只要将不同光源对颜色的影响进行叠加就好。

这里我们为了达到复用的目的，我们将计算颜色的部分抽取成一个函数。代码很简单，我直接放一下片段着色器中的全部代码：

```
#version 330 core

struct Material {
    sampler2D diffuse;
    sampler2D specular;
    float shininess;
};

struct Light {
    vec3  direction;
    vec3 position;
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    float cutOff;
    float outerCutOff;
    float constant;
    float linear;
    float quadratic;
};

#define NR_POINT_LIGHTS 6

///输出颜色
out vec4 FragColor;
///纹理坐标
in vec2 TexCoord;
///法向量
in vec3 normal;
///顶点世界坐标
in vec3 fragPosition;
///观察点
uniform vec3 viewPosition;
///材质
uniform Material material;
///光源
uniform Light pointLight;
uniform Light directionLight;
uniform Light spotLights[NR_POINT_LIGHTS];

///计算定向光的光照颜色（光源、物体材质、观察点）
vec3 CalculateDirectionLight(Light aLight,Material aMaterial,vec3 viewPos);
///计算点光源的光照颜色（光源、物体材质、观察点）
vec3 CalculatePointLight(Light aLight,Material aMaterial,vec3 viewPos);
///计算聚光光源的光照颜色（光源、物体材质、观察点）
vec3 CalculateSpotLight(Light aLight,Material aMaterial,vec3 viewPos);

void main()
{
    ///颜色合成并叠加
    vec3 DirL = CalculatePointLight(pointLight,material,viewPosition);
    DirL += CalculateDirectionLight(directionLight,material,viewPosition);
    for (int i = 0; i < NR_POINT_LIGHTS; ++i) {
        DirL += CalculateSpotLight(spotLights[i],material,viewPosition);
    }
    
    ///颜色合成
    FragColor = vec4(DirL,1.0);
}

///定向光源
vec3 CalculateDirectionLight(Light aLight,Material aMaterial,vec3 viewPos) {
    ///环境光照
    vec3 ambientColor = aLight.ambient * vec3(texture(aMaterial.diffuse, TexCoord));
    
    ///计算到顶点光线方向
    vec3 norm = normalize(normal);
    vec3 lightDir = normalize(-aLight.direction);
    
    ///计算衰减系数r
    float distance = length(aLight.position - fragPosition);
    float attenuationFactor = 1.0 / (aLight.constant + aLight.linear * distance +
                                     aLight.quadratic * (distance * distance));
    
    ///漫反射光照
    float factor = max(dot(norm,lightDir),0.0);
    vec3 diffuseColor = aLight.diffuse * factor * vec3(texture(aMaterial.diffuse, TexCoord));
    
    ///镜面反射光照
    vec3 reflectDir = reflect(-lightDir, norm);
    vec3 viewDir = normalize(viewPos - fragPosition);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0),aMaterial.shininess);
    vec3 specularColor = aLight.specular * spec * vec3(texture(aMaterial.specular, TexCoord));
    return (ambientColor + diffuseColor + specularColor) * attenuationFactor;
}

///点光源
vec3 CalculatePointLight(Light aLight,Material aMaterial,vec3 viewPos) {
    vec3 ambientColor = aLight.ambient * vec3(texture(aMaterial.diffuse, TexCoord));
    
    ///计算到顶点光线方向
    vec3 norm = normalize(normal);
    vec3 lightDir = normalize(aLight.position - fragPosition);
    
    ///计算衰减系数r
    float distance = length(aLight.position - fragPosition);
    float attenuationFactor = 1.0 / (aLight.constant + aLight.linear * distance +
                                     aLight.quadratic * (distance * distance));
    
    ///漫反射光照
    float factor = max(dot(norm,lightDir),0.0);
    vec3 diffuseColor = aLight.diffuse * factor * vec3(texture(aMaterial.diffuse, TexCoord));
    
    ///镜面反射光照
    vec3 reflectDir = reflect(-lightDir, norm);
    vec3 viewDir = normalize(viewPos - fragPosition);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0),aMaterial.shininess);
    vec3 specularColor = aLight.specular * spec * vec3(texture(aMaterial.specular, TexCoord));
    return (ambientColor + diffuseColor + specularColor) * attenuationFactor;
}


///聚光光源
vec3 CalculateSpotLight(Light aLight,Material aMaterial,vec3 viewPos) {
    ///环境光照
    vec3 ambientColor = aLight.ambient * vec3(texture(aMaterial.diffuse, TexCoord));
    
    ///计算到顶点光线方向
    vec3 norm = normalize(normal);
    vec3 lightDir = normalize(aLight.position - fragPosition);
    
    ///计算衰减系数r
    float distance = length(aLight.position - fragPosition);
    float attenuationFactor = 1.0 / (aLight.constant + aLight.linear * distance +
                                     aLight.quadratic * (distance * distance));
    
    float theta = dot(lightDir, normalize(-aLight.direction));
    float smoothFactor = clamp((theta - aLight.outerCutOff)/(aLight.cutOff - aLight.outerCutOff),0.0,1.0);
    
    ///漫反射光照
    float factor = max(dot(norm,lightDir),0.0);
    vec3 diffuseColor = aLight.diffuse * factor * vec3(texture(aMaterial.diffuse, TexCoord));
    
    ///镜面反射光照
    vec3 reflectDir = reflect(-lightDir, norm);
    vec3 viewDir = normalize(viewPosition - fragPosition);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0),aMaterial.shininess);
    vec3 specularColor = aLight.specular * spec * vec3(texture(aMaterial.specular, TexCoord));
    
    ///颜色合成
    return (ambientColor + (diffuseColor + specularColor) * smoothFactor) * attenuationFactor;
}
```

这部分代码比较简单，这里我们看到spotLights是一个数组，我们放一下在为片段着色器中数组的代码：

```
///设置聚光光源初始属性（模拟手电筒）
    for (int i = 0; i < 6; ++i) {
        std::string str = "spotLights[";
        std::string res = str + std::to_string(i) + "].";

        ourShader.setFloat(res + "cutOff", glm::cos(glm::radians(12.5f)));
        ourShader.setFloat(res + "outerCutOff", glm::cos(glm::radians(17.5f)));
        ourShader.setVec3f(res + "ambient", torch.AmbientColor);
        ourShader.setVec3f(res + "diffuse", torch.DiffuseColor);
        ourShader.setVec3f(res + "specular", torch.SpecularColor);
        ourShader.setFloat(res + "constant", 1.0f);
        ourShader.setFloat(res + "linear", 0.09f);
        ourShader.setFloat(res + "quadratic", 0.032f);

        ourShader.setVec3f(res + "position", torch_positions[i]);
        ourShader.setVec3f(res + "direction", torch_directions[i]);
    }
    
    
   ///这个循环中的写法等价于
   ourShader.setFloat("spotLights[0].cutOff", glm::cos(glm::radians(12.5f)));
```

今天的代码就是一个简单的叠加，这部分详细的内容，可以看下[Demos/6.多光源](https://github.com/CodeWicky/Learning-OpenGL/tree/master/%E5%85%89%E7%85%A7/Demos/6.%E5%A4%9A%E5%85%89%E6%BA%90)中的相关代码。




