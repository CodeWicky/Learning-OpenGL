# 3.材质

在显示生活中，即使两个物体相对于光源的位置一样，观察点一样我们观察到的物体也有可能是不同的，这是因为他们的材质是不同的。不同的材质，对光的反射强度是不同的，对光的散射程度也不尽相同。因此，为了在OpenGL中模拟多种类型的物体，我们必须为每一个物体定义一个`材质`属性。

在上一节中，我们已经知道，我们`给定一个物体实际颜色，以及光源颜色，结合光源颜色的环境分量、漫反射分量及镜面分量即可描述一个物体的视觉输出。`。然而物体本身对光源的这三个分量的影响强度也不同，那么这一章，我们`将物体的颜色也分为环境分量、漫反射分量及镜面分量，我们将之称之为材质颜色`。通过制定材质颜色的每一个分量，我们可以实现对物体颜色的精细控制。当然，不同材质反光度也是不同的，其表现就是镜面高光的散射半径不同，`我们同时还需要一个反光度系数来控制物体颜色`。

```
#version 330 core
struct Material {
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    float shininess;
}; 

uniform Material material;
```

上述我们定义了一个结构体，将三个分量和反光度定义在里面，并在片段着色器中以uniform定义一个该结构体的变量。这样我们就可以从外界传入这个结构体来改变物体的材质了。

Material结构体的存在，简化了我们设置材质的方式。这里有一个表格中介绍了一种常见的材质的各个分量的数值。[devernay.free.fr](http://devernay.free.fr/cours/opengl/materials.html)上的这个表格介绍了几种材质的属性。下面的图片展示了几种现实世界的材质对立方体的影响：

![材质](https://learnopengl-cn.github.io/img/02/03/materials_real_world.png)

可以看到，不同材质的物体的表现实际上是不同的。接下来，我们将在我们的着色器中，实现一个材质系统。

先来看一下代码，首先是我们的main.cpp中设置材质的部分：

```
///设置材
ourShader.setVec3f("material.ambient", glm::vec3(1.f,0.5f,0.31f));
ourShader.setVec3f("material.diffuse", glm::vec3(1.f,0.5f,0.31f));
ourShader.setVec3f("material.specular", glm::vec3(0.5f,0.5f,0.5f));
ourShader.setFloat("material.shininess", 32.0f);
```

然后是我们的片段着色器中的代码：

```
#version 330 core

struct Material {
    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
    float shininess;
};

///输出颜色
out vec4 FragColor;
///法向量
in vec3 normal;
///顶点世界坐标
in vec3 fragPosition;
///光照颜色
uniform vec3 lightColor;
///光照点
uniform vec3 lightPosition;
///观察点
uniform vec3 viewPosition;
///材质
uniform Material material;
void main()
{
    ///环境光照
    vec3 ambientColor = lightColor * material.ambient;

    ///漫反射光照
    vec3 norm = normalize(normal);
    vec3 lightDir = normalize(lightPosition - fragPosition);
    float factor = max(dot(norm,lightDir),0.0);
    vec3 diffuseColor = lightColor * (factor * material.diffuse);
    
    ///镜面反射光照
    vec3 reflectDir = reflect(-lightDir, norm);
    vec3 viewDir = normalize(viewPosition - fragPosition);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0),material.shininess);
    vec3 specularColor = lightColor * (spec * material.specular);
    
    ///颜色合成
    FragColor = vec4((ambientColor + diffuseColor + specularColor) , 1.0);
}
```

可以看出，整体思路还是一样的，`就是计算物体自身颜色在光照颜色下的叠加情况`。上一章我们固化了物体的颜色，分别计算光源在不同分量下对物体颜色的影响；而这里我们的角度更像是固化了光源的颜色，计算光源对物体颜色的三个分量上的影响。**事实上，不管是光源的三个分量，还是物体颜色的三个分量，都应该是我们考虑的角度**，所以我们又引入了光的属性的概念。

### 光的属性

光的属性的概念就是我们上一章所讲述的光源的三个不同分量，不过上一章我们使用环境强度、漫反射强度及镜面强度和光源颜色共同来描述光的属性，这里我们可以对他进行更精细的控制，我们可以用光的三个不同分量具有不同的颜色来面熟光的属性，另外光源的位置也将影响物体的颜色，所以我们将光的位置也作为光的属性。

```
struct Light {
    vec3 position;

    vec3 ambient;
    vec3 diffuse;
    vec3 specular;
};

uniform Light light;
```




